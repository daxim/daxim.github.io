<!doctype html>
<html>
    <head>
    <title>Nonogram assistant</title>
    <style>
        @charset "utf-8";
        /* TO DO make all of those configurable */
        .rhint {text-align: right; height: 2ch;}
        .chint {width: 2ch; text-align: right; vertical-align: bottom;}
        .rhint, .chint, #hints {background: #efe;}
        td[id] {width: 2ch; height: 2ch; text-align: center; vertical-align: middle;}
        /* TO DO check contrast, test against inverted colour scheme */
        td[id].t {background: repeating-linear-gradient(to top left, rgb(192, 192, 255),rgb(192, 192, 255) 5%, white 0%, white 15%);}
        td[id].f {background: repeating-linear-gradient(to bottom left, rgb(255, 192, 192),rgb(255, 192, 192) 5%, white 0%, white 15%);}
        td[id].t:after {content: "‚≠ïÔ∏é";}
        td[id].f:after {content: "‚ùåÔ∏é";}
        tr, td, #cells {border: 0.2ch double black;}
        /* TO DO find a nicer solution to always show these thick borders */
        tr:nth-child(5n+1) td {border-bottom-style: solid;}
        td:nth-child(5n+1) {border-right-style: solid;}
        details {margin: 1ch;}
        tt {background: #eee;}
        form input[type="number"] { width: 4ch; }
    </style>
    <script src="webperl.js"></script>
    <script type="text/perl">
#<![CDATA[
use 5.028;
use utf8;
use strict;
use warnings;
use experimental qw(signatures);
use List::Util qw(all none sum);
use WebPerl qw(js);

# partition :: Int, Int ‚Üí ArrayRef[ArrayRef[Int]]
sub partition($n, $size) {
    my @r;
    my @p = (0, $n);
    my $k = 1;
    while ($k != 0) {
        my $x = $p[$k - 1] + 1;
        my $y = $p[$k] - 1;
        $k--;
        while ($x <= $y and $k < $size - 1) {
            $p[$k] = $x;
            $y -= $x;
            $k++;
        }
        $p[$k] = $x + $y;
        push @r, [@p[0 .. $k]];
    }
    \@r
}

# permute :: ArrayRef[Int], ArrayRef[Int], ArrayRef[Int] ‚Üí ArrayRef[Int]
sub permute($input, $subset = [], $r = []) {
    for my $idx (0 .. $input->@* - 1) {
        my $current = splice $input->@*, $idx, 1;
        push $subset->@*, $current;
        push $r->@*, [$subset->@*] unless $input->@*;
        permute($input, $subset, $r);
        splice $input->@*, $idx, 0, $current;
        pop $subset->@*;
    }
    $r
}

# separators :: Int, ArrayRef[Int] ‚Üí ArrayRef[ArrayRef[Int]]
sub separators($length, $hints) {
    my @r;
    for my $part (
        partition(($length - sum $hints->@*), $hints->@* + 1)->@*
    ) {
        for my $perm (permute($part)->@*) {
            if ($perm->@* == $hints->@* - 1) {
                push @r, [0, $perm->@*, 0];
            } elsif ($perm->@* == $hints->@*) {
                push @r, [0, $perm->@*], [$perm->@*, 0];
            } elsif ($perm->@* == $hints->@* + 1) {
                push @r, $perm;
            }
        }
    }
    \@r
}

# uniq :: ArrayRef[ArrayRef[Int]] ‚Üí ArrayRef[ArrayRef[Int]]
sub uniq($l) {
    my (@r, %seen);
    for my $e ($l->@*) {
        my $key = join ',', $e->@*;
        next if $seen{$key};
        push @r, $e;
        $seen{$key}++;
    }
    \@r;
}

# solutions :: Int, ArrayRef[Int] ‚Üí ArrayRef[ArrayRef[Bool]]
sub solutions($length, $hints) {
    my $r;
    for my $sep (uniq(separators($length, $hints))->@*) {
        my @sep = map { [(0) x $_ ]} $sep->@*;
        my @hints = map { [(1) x $_ ]} $hints->@*;
        my @solution;
        while (@sep) {
            my $h = shift(@hints);
            push @solution, shift(@sep)->@*, $h ? $h->@* : ();
        }
        push $r->@*, \@solution;
    }
    $r
}

# fractions :: ArrayRef[ArrayRef[Bool]] ‚Üí ArrayRef[Num]
sub fractions($solutions) {
    [
        map {
            my $idx = $_;
            sum(map { $_->[$idx] } $solutions->@*) / $solutions->@*
        } 0 .. $solutions->[0]->@* - 1
    ]
}

# worlds_worst_pbm_parser :: Str ‚Üí ArrayRef[ArrayRef[Bool]]
sub worlds_worst_pbm_parser($pbm) {
    my @lines = split /\n/, $pbm;
    my $format = shift @lines;
    die 'not a PBM' if 'P1' ne $format;
    my ($x, $y) = split / /, shift @lines;
    my $linear = join '', map { s/\s//gr } grep { !/^#/ } @lines;
    die 'input over/underflow' if length $linear != $x * $y;
    my @r;
    for (0 .. $y - 1) {
        push @r, [split '', substr $linear, $_ * $x, $x];
    }
    \@r
}

# make_hints :: ArrayRef[ArrayRef[Bool]] ‚Üí Dict[
#     r => ArrayRef[ArrayRef[Int]],
#     c => ArrayRef[ArrayRef[Int]]
# ]
sub make_hints($puzzle) {
    my $ret;
    for my $y ($puzzle->@*) {
        push $ret->{r}->@*, [
            map { length }
            grep { $_ }
            split /0+/,
            join '',
            $y->@*
        ];
    }
    for my $x (0 .. $puzzle->[0]->@* - 1) {
        push $ret->{c}->@*, [
            map { length }
            grep { $_ }
            split /0+/,
            join '',
            map { $_->[$x] }
            $puzzle->@*
        ];
    }
    $ret
}

# grid :: Dict[
#     r => ArrayRef[ArrayRef[Int]],
#     c => ArrayRef[ArrayRef[Int]]
# ] ‚Üí Str
sub grid($hints) {
    my $table = '<table>';
    $table .= '<tr><td></td>';
    while (my ($c, $x) = each $hints->{c}->@*) {
        $table .= qq(<td class="chint" data-col="$c">) . join(' ', $x->@*) . '</td>';
    }
    $table .= "</tr>\n";
    while (my ($r, $y) = each $hints->{r}->@*) {
        $table .= qq(<tr>);
        $table .= qq(<td class="rhint" data-row="$r">) . join(' ', $y->@*) . qq(</td>);
        while (my ($c, $x) = each $hints->{c}->@*) {
            $table .= qq(<td id="r${r}c${c}"></td>);
        }
        $table .= "</tr>\n";
    }
    $table .= '</table>';
    $table
}

# scale :: Tuple[Int, Int, Int], Tuple[Int, Int, Int], Num ‚Üí Tuple[Int, Int, Int]
sub scale($rgb1, $rgb2, $rat) {
    [
        int($rgb2->[0] + ($rgb1->[0] - $rgb2->[0]) * $rat),
        int($rgb2->[1] + ($rgb1->[1] - $rgb2->[1]) * $rat),
        int($rgb2->[2] + ($rgb1->[2] - $rgb2->[2]) * $rat),
    ]
}

# filter :: ArrayRef[ArrayRef[Bool]], ArrayRef[Int], ArrayRef[Int] ‚Üí ArrayRef[ArrayRef[Bool]]
sub filter($solutions, $T, $F) {
    [grep {
        my $s = $_;
        (all { !!$_ } $s->@[$T->@*]) and
        (all {  !$_ } $s->@[$F->@*])
    } $solutions->@*]
}

sub draw($dom, $app) {
    $dom->querySelector('#columnhints')->{value} = join "\n", map { join ' ', $_->@* } $app->{c}->@*;
    $dom->querySelector('#rowhints')->{value} = join "\n", map { join ' ', $_->@* } $app->{r}->@*;
    $dom->querySelector('table')->{innerHTML} = grid($app);
    $dom->querySelectorAll('td[id]')->forEach(sub($elem, $, $) {
        $elem->addEventListener('click', sub($ev) {
            my $class = $ev->{target}{classList};
            if ($class->contains('t')) {
                $class->remove('t');
                $class->add('f');
            } elsif ($class->contains('f')) {
                $class->remove('f');
            } else {
                $class->add('t');
            }
        });
    });
    $dom->querySelectorAll('td.rhint, td.chint')->forEach(sub($elem, $, $) {
        $elem->addEventListener('click', sub($ev) {
            my $t = $ev->{target}{dataset};
            my ($length, $hints);
            if (exists $t->{row}) {
                $hints = $app->{r}[$t->{row}];
                $length = $app->{c}->@*;
            } elsif (exists $t->{col}) {
                $hints = $app->{c}[$t->{col}];
                $length = $app->{r}->@*;
            } else { ... }
            my (@T, @F);
            if (exists $t->{row}) {
                $dom->querySelectorAll(sprintf 'td[id^=r%uc].t', $t->{row})->forEach(sub($elem, $, $) {
                    push @T, $elem->{id} =~ /c(\d+)/
                });
                $dom->querySelectorAll(sprintf 'td[id^=r%uc].f', $t->{row})->forEach(sub($elem, $, $) {
                    push @F, $elem->{id} =~ /c(\d+)/
                });
            } elsif (exists $t->{col}) {
                $dom->querySelectorAll(sprintf 'td[id$=c%u].t', $t->{col})->forEach(sub($elem, $, $) {
                    push @T, $elem->{id} =~ /r(\d+)/
                });
                $dom->querySelectorAll(sprintf 'td[id$=c%u].f', $t->{col})->forEach(sub($elem, $, $) {
                    push @F, $elem->{id} =~ /r(\d+)/
                });
            } else { ... }
            my @solutions = fractions(filter(solutions($length, $hints), \@T, \@F))->@*;
            my @scale = map {
                scale($app->{settings}{true}, $app->{settings}{false}, $_)
            } @solutions;
            if (exists $t->{row}) {
                for my $idx (0 .. @scale - 1) {
                    my $elem = $dom->querySelector(sprintf '#r%uc%u', $t->{row}, $idx);
                    $elem->{style}{border} = sprintf '0.2ch solid rgb(%u,%u,%u)', $scale[$idx]->@*;
                    if ($solutions[$idx] == 1) {
                        $elem->{classList}->remove('f');
                        $elem->{classList}->add('t');
                    } elsif ($solutions[$idx] == 0) {
                        $elem->{classList}->remove('t');
                        $elem->{classList}->add('f');
                    }
                }
            } elsif (exists $t->{col}) {
                for my $idx (0 .. @scale - 1) {
                    my $elem = $dom->querySelector(sprintf '#r%uc%u', $idx, $t->{col});
                    $elem->{style}{border} = sprintf '0.2ch solid rgb(%u,%u,%u)', $scale[$idx]->@*;
                    if ($solutions[$idx] == 1) {
                        $elem->{classList}->remove('f');
                        $elem->{classList}->add('t');
                    } elsif ($solutions[$idx] == 0) {
                        $elem->{classList}->remove('t');
                        $elem->{classList}->add('f');
                    }
                }
            } else { ... }
        });
    });
}

my $app = {
    c => [
        [2], [2, 5], [11], [13], [1, 12], [14], [14, 1], [3, 7, 3], [19],
        [15], [11], [12, 3], [20, 1], [19, 1, 1], [18, 2], [16, 3], [22, 2],
        [21], [16], [13], [15, 3], [23], [8, 1, 2], [5, 3], [5]
    ],
    r => [
        [4], [4, 7], [4, 2, 8], [8, 9], [6, 10], [6, 13], [6, 14], [6, 15],
        [22], [23], [23], [23], [23], [22], [15, 3, 1], [11, 2, 2],
        [2, 3, 2, 2, 1], [2, 3, 2, 2, 1], [2, 4, 2, 2], [2, 3, 2, 1],
        [1, 2, 2, 2], [1, 1, 1, 2], [1, 1, 1, 1], [1, 2, 1], [1, 3, 1],
        [1, 2, 1, 2], [1, 2], [2, 2]
    ],
    # TO DO expose settings in UI
    settings => {
        true => [64, 64, 255],
        false => [255, 64, 64],
    }
};

my $dom = js('document');
$dom->querySelector('form')->addEventListener('submit', sub($ev) {
    $ev->preventDefault;
    $ev->{submitter}->blur;
    my $fieldset = $ev->{submitter}{parentElement}{children};
    if (exists $fieldset->{width} && exists $fieldset->{height}) {
        $app->{c} = [map {[]} 1..$fieldset->{width}{value}];
        $app->{r} = [map {[]} 1..$fieldset->{height}{value}];
        draw($dom, $app);
    } elsif (exists $fieldset->{upload}) {
        $fieldset->{upload}{files}[0]->text->then(sub($pbm) {
            $app = { $app->%*, make_hints(worlds_worst_pbm_parser($pbm))->%* };
            draw($dom, $app);
        });
    } elsif (exists $fieldset->{columnhints} && exists $fieldset->{rowhints}) {
        $app->{c} = [map { [split / /, $_] } grep { !!$_ } split "\n", $fieldset->{columnhints}{value}];
        $app->{r} = [map { [split / /, $_] } grep { !!$_ } split "\n", $fieldset->{rowhints}{value}];
        draw($dom, $app);
    } else { ... }
});
draw($dom, $app);


=pod

TO DO document Web API and CSS requirements (see caniuse)

TO DO implement checking the status of a single cell

TO DO implement suggesting a large/small hint for row/col
(reveals greatest/least amount of cells)

TO DO drag mouse to paint across cells

TO DO keyboard shortcuts for marking cell state

TO DO move cell state from DOM into app state

TO DO improve rendering speed by DOM diffing

TO DO memoise innermost functions to prevent recalculation of known values

TO DO generate a thumbnail image of the nonogram for the top left grid corner

TO DO replace if/else with pattern match

TO DO give duskwuff the middle finger for intellectual dishonesty
(petty downvoting, goal post shifting)

=cut
#]]>
    </script>
</head>
<body>

<table></table>
<details open="open">
      <summary>legend</summary>
    <p>Click <mark id="hints">hints row/column header</mark> to deduce.</p>
    <p>Click <mark id="cells">cells</mark> to toggle cell state through unknown, true, false.</p>
</details>
<details>
    <summary>puzzle set-up</summary>
    <form>
        <fieldset>
            <legend>new empty puzzle with dimensions</legend>
            <label for="width">width</label>
            <input type="number" min="1" id="width">
            <label for="height">height</label>
            <input type="number" min="1" id="height">
            <input type="submit">
        </fieldset>
        <fieldset>
            <legend>new puzzle with hints derived from black-and-white image</legend>
            <label for="upload">
                <a href="https://en.wikipedia.org/wiki/Netpbm#Description">PBM file</a>
            </label>
            <input type="file" accept=".pbm,image/x-portable-bitmap" id="upload">
            <input type="submit">
            <p>üõà try <a href="https://imagemagick.org/">ImageMagick</a> command:
            <tt>convert -compress none input.gif output.pbm</tt></p>
        </fieldset>
        <fieldset>
            <legend>edit hints</legend>
            <label for="rowhints">row hints</label>
            <textarea cols="12" id="rowhints"></textarea>
            <label for="columnhints">column hints</label>
            <textarea cols="12" id="columnhints"></textarea>
            <input type="submit">
            <p>üõà Separate hints with space. Separate hint groups with newline.</p>
        </fieldset>
    </form>
</details>

</body>
</html>
